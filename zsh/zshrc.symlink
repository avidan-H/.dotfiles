#!/bin/bash

# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=$HOME/.dotfiles
export PRIVATE_DOTFILES_ROOT=$HOME/.private_dotfiles

# your project folders that we can `c [tab]` to
export PROJECTS="$HOME"/dev
export PERSONAL_PROJECTS="$HOME"/localdev

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=("$DOTFILES_ROOT"/**/*.zsh "$PRIVATE_DOTFILES_ROOT"/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files


# all of our custom shell commands
typeset -U custom_shell_cmds
custom_shell_cmds=("$DOTFILES_ROOT"/bin/*)
if [[ -d  "$PRIVATE_DOTFILES_ROOT"/bin ]]; then
  custom_shell_cmds+=("$PRIVATE_DOTFILES_ROOT"/bin/*)
fi

# symlink shell script commands from bin dirs to /usr/local/bin
for file in ${custom_shell_cmds}; do
  file_name=${file##*/}
  symlink=/usr/local/bin/"$file_name"
  if [[ ! -h "$symlink" ]]; then
    ln -s "$file" "$symlink"
  fi
done

unset custom_shell_cmds


# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
